
apply plugin: 'com.moowork.grunt'
apply plugin: 'gwt-base'

dependencies {
    compile project(':core:promises')
    compile libraries.gwt.user
    compile 'org.gwtbootstrap3:gwtbootstrap3:0.9.1'
}

node {
    // Version of node to use.
    version = '0.10.25'
    npmVersion = '2.1.5'
    download = true
}

grunt {
    // Set the directory where Gruntfile.js should be found
    workDir = project.projectDir

    // Whether colors should output on the terminal
    colors = false

    // Whether output from Grunt should be buffered - useful when running tasks in parallel
    bufferOutput = false
}

// Work around for
// https://github.com/srs/gradle-grunt-plugin/issues/32
installGrunt.setArgs( ['install', 'grunt-cli@1.2.0', 'grunt@0.4.5'] )

jar {
    from project.sourceSets.main.allSource
    from project.sourceSets.main.output
    from "${buildDir}/css"
}

task compileCss(type: GruntTask) {
    args = ["build"]
    dependsOn 'npmInstall'
    dependsOn 'installGrunt'

    inputs.file "${projectDir}/Gruntfile.js"
    inputs.dir "${projectDir}/src/main/less"
    outputs.dir "${buildDir}/css"
}

jar.dependsOn compileCss


idea {
    module {
        scopes.PROVIDED.plus += [ configurations.gwt ]
    }
}

