
description = 'ActivityInfo API Documentation'

ext {
    outputDir = "$buildDir/html"
}

sourceSets {
    // This source set *generates* the client code for GWT
    generator {
        java {
            srcDir 'src/generator/java'
        }
        resources {
            srcDir 'src/generator/resources'
        }
    }
}

dependencies {
    generatorCompile libraries.guava
    generatorCompile 'io.swagger:swagger-parser:1.0.19'
    generatorCompile 'org.pegdown:pegdown:1.6.0'
    generatorCompile libraries.freemarker
    generatorCompile project(':core:model')
}

task compileSass(type: org.kravemir.gradle.sass.SassCompileTask) {
    srcDir = file("$projectDir/src/main/style")
    outDir = file("$buildDir/html/assets")
    exclude = "_*"
}

task copyAssets(type: Copy) {
    from('src/main/assets') 
    into "$outputDir/assets"
}

task generateDocs(type: JavaExec) {
    group = "API"
    description = 'Generates API Documentation'

    dependsOn 'generatorClasses'
    dependsOn ':api:compileSpec'

    main = 'org.activityinfo.api.tools.DocGenerator'
    classpath = sourceSets.generator.runtimeClasspath
    workingDir = rootProject.projectDir
    args tasks.getByPath(':api:compileSpec').outputFile
    args outputDir
}

build.dependsOn copyAssets
build.dependsOn compileSass
build.dependsOn generateDocs