
plugins {
    id "java"
    id "war"
}

configurations {
    gwtSdk {
        extendsFrom compile
    }
}

dependencies {
    compile project(':model')
    compile project(':store:apiserver')

    compile libraries.jersey.servlet

    compile 'com.zaxxer:HikariCP:2.7.2'
    compile 'mysql:mysql-connector-java:5.1.44'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'

    gwtSdk "com.google.gwt:gwt-dev:2.8.0"
}

war {
    /* Include compiled Javascript application */
    from(project(':chdc:frontend').outputDir) {
        exclude("**/*.symbolMap")
    }
}

tasks.war.dependsOn ':chdc:frontend:compileApp'

task prepareDevMode(type: Copy) {
    into "$buildDir/devMode"
    from "src/main/webapp"
}


task devMode(type: JavaExec) {

    dependsOn prepareDevMode
    dependsOn classes

    classpath project(':chdc:frontend').buildDir.absolutePath + "/theme"
    classpath project(':chdc:frontend').sourceSets.main.java.srcDirs
    classpath project(':chdc:frontend').sourceSets.main.resources.srcDirs
    classpath project(':chdc:frontend').sourceSets.main.runtimeClasspath

    classpath sourceSets.main.runtimeClasspath

    main = 'com.google.gwt.dev.DevMode'

    // Enable debugging
    jvmArgs "-Xdebug"
    jvmArgs "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000"

    args '-startupUrl', 'demo.html'
    args '-war', "$buildDir/devMode"

    args 'chdc.frontend.Chdc'

}